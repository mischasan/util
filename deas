#!/usr/bin/env perl
use strict;
use warnings;
use Data::Dumper;

sub demangle { `c++filt @_` }
sub slurpv { open my $fh, $_[0] or die "cannot open $_[0]"; [<$fh>]; }

for my $fname (@ARGV) {
    my @file;       # maps fileno(1...) to [file_lines]
    my $src = slurpv $fname;

    # Calculate simpler labels for jumps.
    # "F.." if a label is only the target of fwd jumps.
    # "B.." if a label is only the target of back jumps.
    # "L.." otherwise

    my @labdef;     # label definitions in order of occurrence.
    my %labdef;     # indicates a label is defined or not (at time of use)
    my %labuse;     # indicates F and B uses of a label.

    for (@$src) {
        if (m{^(\.?L[0-9A-Z]*):}) {     # ascending numbers for labels
            push @labdef,$1;
            $labdef{$1} = 1;
        } elsif (m{\tj[bplasmogen]\t(\.?L[0-9A-Z]*)$}) { # jump => interesting label
            $labuse{$1} = ($labuse{$1} || '') . ($labdef{$1} ? 'B' : 'F');
        } elsif (!m{ "/usr/|<built-in>} && m{\.file (\d+) "([^"]*)"}) {   # source file declaration
            # gcc 4.3.4 may emit "file N <built-in>"
            $file[$1] = -f $2 ? slurpv $2 : [];
            s/^\s*}\s*$|.*(?:__builtin|__extension).*// for @{$file[$1]};
        } else {
            s{^(_\w+):}{demangle $1}e;
            s{((?:call|jmp)\s+)(_\w+)}{$1.demangle $2}e;
        }
    }
    s{.*FB.*}{L} for values %labuse;
    @labdef = grep {$labuse{$_}} @labdef;
    %labdef = map {$labdef[$_] => substr($labuse{$labdef[$_]},0,1) . $_} 0..$#labdef;
    #$labdef{$_} = substr($labuse{$_},0,1) . $labdef{$_} for grep keys %labuse;

    # Reprocess output, inserting source lines and replacing labels:
  
    open my $out, ">$fname.tmp" or die "$fname.tmp: cannot create";

    my $skip;
    for (@$src) {
        next if m{^#APP|^#NO|^# .*""};

        # Eliminate C++ template output: /^_ZN\S+:$/,/^\w/-1
        $skip = 0 if m{^_ZN}; $skip = 1 if m{^_ZNSt\S+:$};
        next if $skip;

        # Simplify mangled names:
        #XXX properly decode <count><chars>... DNS-like encoding
        s{\b_Z[KNSt]+\d+}{};
        s{(-?\d{5,20}(?!\(%[a-z]*bp))}{sprintf "0x%X",$1}eg;

        if (m{\.loc\s+(\d+)\s+(\d+)}) { # source file reference
            my $n = $2;
            s{^\s*}{#[$n]\t} if $_ = $file[$1][$n - 1] and m{\w};
            $file[1][$n - 1] = '';
        } elsif (m{^(\.?L[0-9A-Z]*):}) {    # <label>:
            $_ = exists $labdef{$1} ? "$labdef{$1}:" : "";
        } elsif (my ($L) = m{\tj[bplasmogen]*\t(\.?L[0-9A-Z]*)$}) {  # jump <label>
            s{$L}{$labdef{$L}} if $labdef{$L};
        }
        print $out $_ if ($_ && !m{^L[^0-9]} && !m{^\t?\.} && !m<^[\t\n {};]*$>sg);
    }

    print $out "# vim: set ts=8 :\n";
    close $out;

    rename "$fname.tmp", $fname;
}
